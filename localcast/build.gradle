apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

repositories {
    maven {
        url  "http://dl.bintray.com/ckurtm/maven"
    }
}

version = "1.1.1"
group = "com.peirr"

ext {
    versionName = '1.1.1'
    groupId = "com.peirr"
    appId = "localcast"
}

def siteUrl = 'https://github.com/ckurtm/LocalCast'
def gitUrl = 'https://github.com/ckurtm/LocalCast.git'


android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"
    publishNonDefault false

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 10
        versionName "1.1.1"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.key")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "LocalCast"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}


install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'LocalCast makes it easy to cast your local files to a connected cast device. Check the sample app for examples'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id "localcast"
                        name 'Kurt Mbanje'
                        email 'ckurtm@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}


dependencies {
    compile 'com.peirr:simplecast:1.0.1'
    compile 'com.peirr.http:http_service:1.9'
}
